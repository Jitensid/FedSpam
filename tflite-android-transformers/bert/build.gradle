apply plugin: 'com.google.protobuf'
apply plugin: 'com.android.application'

android {

    compileSdkVersion 30
    defaultConfig {
        applicationId "co.huggingface.android_transformers.bert"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    androidResources {
        noCompress 'tflite'
    }
    lint {
        abortOnError false
    }
    // If you find lint problem like:
    // * What went wrong:
    // A problem was found with the configuration of task ':app:lint'.
    // > No value has been specified for property 'lintClassPath'.
    //
    // Probably you haven't set ANDROID_HOME. To fix:
    // export ANDROID_HOME=$HOME/Android/Sdk   # Your Android SDK path.
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.0' }
    plugins {
        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.27.2' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { // Options added to --grpc_out
                    option 'lite' }
            }
        }
    }
}

// Download TF Lite model.
apply from: 'download.gradle'

def modelUrl = 'https://drive.google.com/uc?export=download&id=1Nx1_rXUpUbWqS9iZIqvk0xKf3V-wMrwM&confirm=t'
def modelArchivePath = "${buildDir}/model.zip"
def modelTargetLocation = "${projectDir}/src/main/assets/model"

task downloadModelAndData(type: DefaultTask){
    doFirst {
        println "Downloading and unpacking the model..."
        mkdir project.buildDir
    }

    doLast {
        ant.mkdir(dir: modelTargetLocation)

        // If you want to use your own models rather than pre-built models,
        // comment out the following three lines.
        ant.get(src: modelUrl, dest: modelArchivePath)
        ant.unzip(src: modelArchivePath, dest: modelTargetLocation)
        ant.delete(file: modelArchivePath)

    }
}

preBuild.dependsOn downloadModelAndData

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Import tflite dependencies
    implementation 'org.tensorflow:tensorflow-lite:0.0.0-nightly-SNAPSHOT'
    // The GPU delegate library is optional. Depend on it as needed.
    implementation 'org.tensorflow:tensorflow-lite-gpu:0.0.0-nightly-SNAPSHOT'
    implementation 'org.tensorflow:tensorflow-lite-support:0.0.0-nightly-SNAPSHOT'
    // This dependency adds the necessary TF op support.
    implementation 'org.tensorflow:tensorflow-lite-select-tf-ops:0.0.0-nightly-SNAPSHOT'

    implementation 'io.grpc:grpc-okhttp:1.45.1' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-protobuf-lite:1.45.1' // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.45.1' // CURRENT_GRPC_VERSION

    implementation 'com.univocity:univocity-parsers:2.9.1' // CSV File Reader

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:28.1-android'
    implementation project(path: ':transfer_api')

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.opencsv:opencsv:5.6'

    testImplementation 'junit:junit:4.12'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'com.google.truth:truth:1.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

repositories {
    mavenCentral()
}


